apply plugin: 'com.android.application'
apply from: 'build-helpers.gradle'

android {
    signingConfigs {
        zop {
            keyAlias 'zopapp-release'
            keyPassword 'z009Uwe'
            storeFile file('/Volumes/STAFF-SAAD/Users/khawar/dev_area/learninglayers/app_release/zopapp-release-key.keystore')
            storePassword 'z009Uwe'
        }
    }
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    useLibrary 'org.apache.http.legacy'
    defaultConfig {
        applicationId 'fi.aalto.legroup.zop'
        minSdkVersion 16
        targetSdkVersion 23
        multiDexEnabled true
        generatedDensities = []
        (versionName, versionCode) = project.getGitVersions()
        signingConfig signingConfigs.zop
    }
    dexOptions {
        javaMaxHeapSize '4g'
    }
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }
    buildTypes {
        /*release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }*/
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig getSigningConfig()
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def date = new Date();
                    def formattedDate = date.format('yyyyMMddHHmmss')
                    output.outputFile = new File(output.outputFile.parent,
                            output.outputFile.name.replace("-release", "-" + formattedDate)
                            //for Debug use output.outputFile = new File(output.outputFile.parent,
                            //                             output.outputFile.name.replace("-debug", "-" + formattedDate)
                    )
                }
            }
        }
    }
    lintOptions {
        abortOnError false
    }
    productFlavors {
    }
}

repositories {
    mavenCentral()

    // An additional repo for the barcode scanner
    maven {
        url 'https://raw.github.com/embarkmobile/zxing-android-minimal/mvn-repo/maven-repository/'
    }

    // For com.github.afollestad.material-dialogs
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Android backwards-compatibility
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:support-v4:23.2.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.google.android.exoplayer:exoplayer:r1.3.3'
    // Material design dialogs
    compile('com.github.afollestad.material-dialogs:commons:0.8.5.4@aar') {
        transitive = true
    }
    // Material design tab strip. Note that this is jpardogo's fork, not the original version
    // that's no longer updated. The class name of the view is still same, though.
    // See https://github.com/jpardogo/PagerSlidingTabStrip
    compile 'com.jpardogo.materialtabstrip:library:1.0.9'
    // Material design floating action button
    compile 'com.melnykov:floatingactionbutton:1.2.0'
    // Material design snackbars
    compile 'com.nispok:snackbar:2.10.6'
    // Nice core libraries for a multitude of tasks
    compile 'com.google.guava:guava:18.0'
    // Google Play Services APIs
    compile 'com.google.android.gms:play-services-base:8.1.0'
    compile 'com.google.android.gms:play-services-maps:8.1.0'
    compile 'com.google.android.gms:play-services-location:8.1.0'
    compile 'com.google.android.gms:play-services-analytics:8.1.0'
    // OAuth2 library for OpenID Connect
    compile('com.google.oauth-client:google-oauth-client:1.19.0') {
        exclude group: 'com.google.android'
        exclude group: 'junit'
        exclude group: 'xpp3'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    // Barcode scanning
    compile 'com.embarkmobile:zxing-android-minimal:2.0.0@aar'
    compile 'com.embarkmobile:zxing-android-integration:2.0.0@aar'
    compile 'com.google.zxing:core:3.0.1'
    // JSON parsing
    compile('com.google.api-client:google-api-client-gson:1.19.0') {
        exclude group: 'com.google.guava', module: 'guava-jdk5'
    }
    // MPEG-4 parsing
    compile 'com.googlecode.mp4parser:isoparser:1.0.5.4'
    // Image manipulation
    compile 'com.squareup.picasso:picasso:2.4.0'
    // Better and more resilient HTTP requests
    compile 'com.squareup.okhttp:okhttp:2.1.0'
    // REST-client
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.1.0'
    compile 'com.squareup.retrofit:retrofit:1.7.1'
    // Event bus
    compile 'com.squareup:otto:1.3.5'
    // Date and time formatting
    compile 'joda-time:joda-time:2.3'
    // Decrypting the Layers Box URL
    compile 'fi.aalto.legroup:cryptohelper:0.1.0'
}
